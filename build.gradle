group = 'com.webank.webase.node.mgr'
version = '1.0.0-SNAPSHOT'

println "======Gradle version:" + gradle.gradleVersion
if (gradle.gradleVersion.startsWith("7")) {
    println "Gradle 7.x not support yet!\n ====== please use Gradle version from 4.6.x to 6.9.x "
} else if (gradle.gradleVersion.startsWith("6")
        || gradle.gradleVersion.startsWith("5")
        || gradle.gradleVersion.startsWith("4.10")
        || gradle.gradleVersion.startsWith("4.9")
        || gradle.gradleVersion.startsWith("4.8")
        || gradle.gradleVersion.startsWith("4.7")
) {
    println "Gradle with version >= 4.6 detected"
} else {
    println "Gradle with version < 4.6 detected"
}


apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'


sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// In this section you declare where to find the dependencies of your project
repositories {
    maven {url "http://maven.aliyun.com/nexus/content/groups/public/"}
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://dl.bintray.com/ethereum/maven/' }
    mavenLocal()
}

def springboot_version="2.7.10"
List springboot = [
     "org.springframework.boot:spring-boot-starter-web:$springboot_version",
     "org.springframework.boot:spring-boot-starter-security:$springboot_version",
     "org.springframework.boot:spring-boot-starter-mail:$springboot_version",
     "org.springframework.boot:spring-boot-starter-thymeleaf:$springboot_version",
     "org.springframework.boot:spring-boot-starter-aop:$springboot_version",
     "org.springframework.boot:spring-boot-starter-validation:$springboot_version",
     "org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.4"
]

List jaxb = [
     "javax.xml.bind:jaxb-api:2.3.0",
     "com.sun.xml.bind:jaxb-impl:2.3.0",
     "com.sun.xml.bind:jaxb-core:2.3.0",
     "javax.activation:activation:1.1.1"
]

List lombok = [
    "org.projectlombok:lombok:1.18.12"
]

def jackson_version = "2.14.2"
List jackson = [
    "com.fasterxml.jackson.core:jackson-databind:$jackson_version",
    "com.fasterxml.jackson.core:jackson-annotations:$jackson_version",
    "com.fasterxml.jackson.core:jackson-core:$jackson_version",
    "com.fasterxml.jackson.module:jackson-module-parameter-names:$jackson_version",
    "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_version",
    "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version",
]

def log4j_version="2.18.0"
List log4j = [
    "org.apache.logging.log4j:log4j-api:$log4j_version",
    "org.apache.logging.log4j:log4j-core:$log4j_version",
    "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version",
    "org.apache.logging.log4j:log4j-web:$log4j_version"
]

List mbg = [
    'org.mybatis.generator:mybatis-generator-core:1.3.2',
    'tk.mybatis:mapper:3.3.2',
    'com.github.yuanmomo:mybatis-generator-plugin:1.0.9'
]

List scaffold = [
    'com.webank.webase:solscaffold:3.3.0-SNAPSHOT',
    'com.webank:WeBankBlockchain-SmartDev-Scaffold:3.3.0-SNAPSHOT',
    'com.webank:solc-gradle-plugin:3.3.0-SNAPSHOT'
]

List swagger = [
    'io.springfox:springfox-swagger2:2.8.0',
    'io.springfox:springfox-swagger-ui:2.8.0'
]

List commons = [
    "org.apache.commons:commons-lang3:3.8.1",
    'org.apache.commons:commons-collections4:4.4',
    'org.apache.commons:commons-configuration2:2.8.0',
    'org.apache.commons:commons-compress:1.21'
]

dependencies {
    compile springboot,jaxb,jackson,log4j,scaffold,swagger,commons
    compile ('org.fisco-bcos.java-sdk:fisco-bcos-java-sdk:3.3.0') {
        exclude group: "org.slf4j"
    }
    runtime "mysql:mysql-connector-java:8.0.28"
    compile "com.zaxxer:HikariCP:3.2.0"
    compile 'org.bouncycastle:bcprov-jdk15on:1.69'
    compile 'javax.validation:validation-api:2.0.1.Final'
    // cover old version
    compile 'com.google.guava:guava:30.0-jre'
    compile 'org.slf4j:jcl-over-slf4j:1.7.30'
    compile 'org.yaml:snakeyaml:2.0'
    compile 'org.apache.commons:commons-text:1.10.0'

    compile 'com.jcraft:jsch:0.1.55'
    compile 'org.ini4j:ini4j:0.5.4'
    compile 'org.thymeleaf:thymeleaf:3.0.11.RELEASE'

    compileOnly lombok
    annotationProcessor lombok

    testCompile ("com.webank:webase-app-sdk:1.5.5")
    testCompile mbg
    testCompileOnly lombok
    testAnnotationProcessor lombok
    testCompile("org.springframework.boot:spring-boot-starter-test:$springboot_version") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testCompile 'junit:junit:4.13.1'
}

configurations {
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.ow2.asm', module: 'asm'
    all*.exclude group: 'jline', module: 'jline'
    all*.exclude group: 'com.google.protobuf', module: 'protobuf-java'
    all*.exclude group: 'javax.annotation', module: 'javax.annotation-api'
    all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'

}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

clean {
    delete 'dist'
    delete 'build'
    delete 'log'
}


jar {
    destinationDir file('dist/apps')
    archiveName project.name + '.jar'
    exclude '**/*.xml'
    exclude '**/*.yml'
    exclude '**/*.properties'

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf_template'
        }
        copy {
            from file('gradle')
            into 'dist/gradle'
        }
        copy {
            from file('script/')
            into 'dist/script'
        }
        copy {
            from configurations.runtime
            into 'dist/lib'
        }
        copy {
            from file('.').listFiles().findAll{File f -> (f.name.endsWith('.sh') || f.name.endsWith('.env'))}
            into 'dist'
        }
        copy {
            from file('release_note.txt')
            into 'dist'
        }
    }
}
